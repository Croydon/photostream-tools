apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "net.saliman.cobertura"
apply plugin: 'com.github.kt3k.coveralls'

group = 'hochschuledarmstadt.photostream_tools'
version = '0.0.86'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    lintOptions {
        abortOnError false
    }


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def siteUrl = 'https://github.com/aschattney/photostream-tools'
def gitUrl = 'https://github.com/aschattney/photostream-tools.git'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = 'photostream-tools'
        desc = 'Classes and utility functions which encapsulate application logic of the PhotoStream Android App'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/aschattney/photostream-tools/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['aar', 'photostream', 'photostream-tools', 'hda', 'praktikum', 'nzse', 'hochschule darmstadt']
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'photostream-tools'
                url siteUrl
                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'aschattney'
                        name 'Andreas Schattney'
                        email 'a.schattney@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/aschattney/photostream-tools.git'
                    developerConnection 'https://github.com/aschattney/photostream-tools.git'
                    url siteUrl

                }
            }
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

tasks.coveralls {
    dependsOn 'check'
}

cobertura.coverageFormats = ['html','xml']

cobertura {
    coverageExcludes = [
                        '.*\\\\.package-info.*', '.*\\.R.*', '.*BuildConfig.*',
                        '.*hochschuledarmstadt\\.photostream_tools\\.callback.*',
                        '.*DividerItemDecoration',
                        '.*hochschuledarmstadt\\.photostream_tools\\.model.*',
                        '.*Http.*',
                        '.*ImageCacher.*',
                        '.*FullscreenPhotoActivity.*',
                        '.*hochschuledarmstadt\\.photostream_tools\\.adapter\\.LruBitmapCache.*',
                        '.*hochschuledarmstadt\\.photostream_tools\\.adapter\\.BitmapLoaderTask.*',
                        '.*hochschuledarmstadt\\.photostream_tools\\.adapter\\.Plugin.*',
                        '.*hochschuledarmstadt\\.photostream_tools\\.adapter\\.PluginAlertDialog.*',
                        '.*hochschuledarmstadt\\.photostream_tools\\.adapter\\.PluginContextualActionBar.*',
                        '.*hochschuledarmstadt\\.photostream_tools\\.adapter\\.AsyncDrawable.*',
                        '.*PhotoStreamClientDelegate.*',
                        '.*PhotoStreamActivity.*',
                        '.*BaseFragmentPagerAdapter.*',
                        '.*BasePhotoPagerAdapter.*',
                        '.*BitmapUtils.*',
                        '.*Plugin.*',
                        '.*PhotoStreamService.*',
                        '.*PhotoStreamClientImpl.*',
                        '.*WebSocket.*',
                        '.*hochschuledarmstadt\\.photostream_tools\\.widget.*',
                        '.*AndroidSocket.*',
                        '.*PhotoStreamFragment.*']
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.1'
    testCompile 'org.mockito:mockito-core:2.0.43-beta'
    compile 'com.google.code.gson:gson:2.4'
    compile ('io.socket:socket.io-client:0.6.1') {
        exclude group: 'org.json', module: 'json'
    }
    compile 'com.github.chrisbanes:PhotoView:1.3.0'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:recyclerview-v7:24.1.1'
}
